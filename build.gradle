
plugins {
    id 'java-library'
    id 'signing'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'io.freefair.lombok' version '6.2.0'
}


repositories {
    mavenCentral()
}


group 'org.chronos-eaas'
archivesBaseName = "chronos-agent"
version '2.3.2-SNAPSHOT'

description = "Chronos Agent"


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}


dependencies {
    /* ChronosJob.ChronosJob(org.json.JSONObject) is exposed to overwriting classes */
    api group: 'org.json', name: 'json', version: '20160212' /* version '20160212' is used by com.mashape.unirest:unirest-java:1.4.9 */

    implementation group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'

    implementation group: 'commons-validator', name: 'commons-validator', version: '1.6'
    implementation group: 'commons-net', name: 'commons-net', version: '3.6'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'

    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.2'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


jar {
    manifest {
        attributes 'Manifest-Version': '1.0'
        attributes 'Copyright': 'The Chronos Project'
        attributes 'Group': project.group
        attributes 'Name': project.name
        attributes 'Version': project.version
    }
}
shadowJar {
    archiveClassifier.set('shadow')
}
assemble.dependsOn shadowJar


signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign configurations.archives
}


javadoc {
    failOnError = false
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact jar
            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            artifact source: javadocJar, classifier: 'javadoc', extension: 'jar'
            pom {
                name = 'Chronos Agent'
                description = 'Reference implementation of a Chronos Agent.'
                url = 'https://chronos-eaas.org/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    url = 'https://github.com/Chronos-EaaS/Chronos-Agent/'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/chronos-eaas/chronos-agent"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
task generatePom(group: 'publishing', dependsOn: "generatePomFileFor${project.name.capitalize()}Publication")


idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        inheritOutputDirs = false
        outputDir = file("${project.buildDir}/classes/main")
        testOutputDir = file("${project.buildDir}/classes/test")
    }
}
